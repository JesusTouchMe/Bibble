import std.Arrays;
import std.Math;
import std.Trap;

abstract class InStream {
    virtual void close() {}

    abstract byte read();

    virtual long read(byte[] buffer, long offset, long count) {
        if (buffer == null) {
            Trap::nullReference();
            return -1; // TODO: noreturn function annotation
        }
        if (offset < 0 || count < 0 || offset + count > buffer.length) {
            Trap::outOfBounds();
            return -1; // TODO: noreturn function annotation
        }

        long i = 0;
        for (; i < count; i++) {
            byte val = read();
            buffer[offset + i] = val;
        }

        return i;
    }

    virtual long read(byte[] buffer) {
        return read(buffer, 0, buffer.length);
    }

    abstract long available();

    virtual long skip(long amount) {
        long remaining = amount;
        byte[] buffer = new byte[2048];

        while (remaining > 0) {
            long toRead = Math::min(buffer.length, remaining);
            long count = read(buffer, 0, toRead);
            if (count == -1) break;
            remaining -= count;
        }

        return amount - remaining;
    }
}

abstract class OutStream {
    virtual void close() {}

    virtual void flush() {}

    abstract void write(byte b);

    virtual void write(view<byte[]> bytes, long offset, long count) {
        if (bytes == null) {
            Trap::nullReference();
            return; // TODO: noreturn function annotation
        }
        if (offset < 0 || count < 0 || offset + count > bytes.length) {
            Trap::outOfBounds();
            return; // TODO: noreturn function annotation
        }

        for (long i = 0; i < count; i++) {
            write(bytes[offset + i]);
        }
    }

    virtual void write(view<byte[]> bytes) {
        write(bytes, 0, bytes.length);
    }
}

class BufferedInStream extends InStream {
    protected InStream in;
    protected byte[] buffer;
    protected int count; // we can use int because the buffer size is limited to int in the constructor
    protected int position;

    BufferedInStream(InStream in, int bufferSize) {
        if (in == null) {
            Trap::nullReference("InStream");
            return; // TODO: noreturn function annotation
        }
        if (bufferSize <= 0) {
            Trap::illegalArgument("bufferSize <= 0");
            return; // TODO: noreturn function annotation
        }

        this.in = in;
        this.buffer = new byte[bufferSize];
        this.count = 0;
        this.position = 0;
    }

    private void fill() {
        if (in == null) {
            Trap::ioError("Stream closed");
            return; // TODO: noreturn function annotation
        }
        count = in.read(buffer, 0, buffer.length);
        position = 0;
    }

    void close() override {
        var input = in;
        if (input != null) {
            in = null;
            buffer = null;
            input.close();
        }
    }

    byte read() override {
        if (position >= count) {
            fill();
            if (count == -1) return -1;
        }

        return buffer[position++];
    }

    long read(byte[] buffer, long offset, long count) override {
        if (buffer == null) {
            Trap::nullReference();
            return 0; // TODO: noreturn function annotation
        }
        if (offset < 0 || count < 0 || count > buffer.length - offset) {
            Trap::outOfBounds();
            return 0; // TODO: noreturn function annotation
        }
        if (count == 0) return 0;

        long totalRead = 0;
        while (count > 0) {
            int available = this.count - position;
            if (available <= 0) {
                fill();
                available = this.count;
                if (available == -1) {
                    if (totalRead == 0) return -1;
                    else return totalRead;
                }
            }

            int toCopy = Math::min(available, count);
            Arrays::copy(this.buffer, position, buffer, offset, toCopy);

            position += toCopy;
            offset += toCopy;
            count -= toCopy;
            totalRead += toCopy;

            if (in.available() == 0) break;
        }

        return totalRead;
    }

    long available() override {
        if (in == null) Trap::ioError("Stream closed");
        return (count - position) + in.available();
    }

    long skip(long amount) {
        if (amount <= 0) return 0;

        int available = count - position;
        if (available <= 0) {
            fill();
            available = count;
            if (available == -1) return 0;
        }

        long skipped = Math::min(available, amount);
        position += skipped;
        return skipped;
    }
}

class BufferedOutStream extends OutStream {
    protected OutStream out;
    protected byte[] buffer;
    protected int count; // can be int since we limit buffer size to int in constructor

    BufferedOutStream(OutStream out, int bufferSize) {
        if (out == null) {
            Trap::nullReference("OutStream");
            return; // TODO: noreturn function annotation
        }
        if (bufferSize <= 0) {
            Trap::illegalArgument("bufferSize <= 0");
            return; // TODO: noreturn function annotation
        }

        this.out = out;
        this.buffer = new byte[bufferSize];
        this.count = 0;
    }

    private void flushBuffer() {
        if (count > 0) {
            out.write(buffer, 0, count);
            count = 0;
        }
    }

    void close() override {
        var output = out;
        if (output != null) {
            out = null;
            buffer = null;
            output.flush();
            output.close();
        }
    }

    void flush() override {
        flushBuffer();
        out.flush();
    }

    void write(byte b) override {
        if (count >= buffer.length) {
            flushBuffer();
        }
        buffer[count++] = b;
    }

    void write(view<byte[]> buffer, long offset, long count) override {
        if (buffer == null) {
            Trap::nullReference();
            return; // TODO: noreturn function annotation
        }
        if (offset < 0 || count < 0 || count > buffer.length - offset) {
            Trap::outOfBounds();
            return; // TODO: noreturn function annotation
        }

        if (count == 0) return;

        if (count >= this.buffer.length) {
            flushBuffer();
            out.write(buffer, offset, count);
            return;
        }

        if (this.count + count > this.buffer.length) {
            flushBuffer();
        }

        Arrays::copy(buffer, offset, this.buffer, this.count, count);
        this.count += count;
    }
}